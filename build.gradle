plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'org.hidetake.swagger.generator' version '2.18.1'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
}

group = 'de.mthix.footloose'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    swaggerUI 'org.webjars:swagger-ui:3.10.0'
    swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation  'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.1'
    //implementation "org.flywaydb:flyway-core:6.0.8"
    implementation 'io.swagger:swagger-annotations:1.5.24'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:2.1.1'
    testImplementation 'io.github.kostaskougios:cloning:1.10.0'

    testRuntime 'com.h2database:h2:1.4.200'
}

swaggerSources {
    // currently swagger-codegen will generate default-methods when java8 is used; this is avoided by two separate configs;
    // see https://stackoverflow.com/questions/50178415/how-to-avoid-default-method-implementation-in-swagger-codegen-interface?rq=1
    footlooseModel {
        inputFile = file('src/main/swagger/FootlooseApi.yaml')
        code {
            language = 'spring'
            configFile = file('src/main/swagger/FootlooseModelConfig.json')
            components = ['models']
            //dependsOn validation - not available for OPenAPI 3.0.2 as of Swagger CodeGen Plugin 2.18.1
        }
    }
    footlooseService {
        inputFile = file('src/main/swagger/FootlooseApi.yaml')
        code {
            language = 'spring'
            configFile = file('src/main/swagger/FootlooseServiceConfig.json')
            components = ['apis']
            //dependsOn validation - not available for OPenAPI 3.0.2 as of Swagger CodeGen Plugin 2.18.1
        }
    }
}

compileJava.dependsOn swaggerSources.footlooseModel.code, swaggerSources.footlooseService.code
sourceSets.main.java.srcDir "${swaggerSources.footlooseModel.code.outputDir}/src/main/java"
sourceSets.main.java.srcDir "${swaggerSources.footlooseService.code.outputDir}/src/main/java"

testSets {
    contextTest     // mocked integration tests under Spring's application context
    integrationTest // full-stack integration tests
}

tasks.withType(Test) {
    useJUnitPlatform()
    forkEvery 1 // forces clearing of H2 db for integration tests
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test